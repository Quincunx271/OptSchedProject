cmake_minimum_required(VERSION 3.4.3)

project(OptSchedPrj)

option(OPTSCHEDPRJ_FLANG "If it is checked-out, build flang." ON)
option(OPTSCHEDPRJ_IGNORE_FLANG "Ignore warnings about flang not existing" OFF)
set(OPTSCHEDPRJ_FLANG_INSTALL_PREFIX ${CMAKE_INSTALL_PREFIX} CACHE PATH "The directory to install flang")

set(OPTSCHEDPRJ_FLANG_BUILD_TYPE Release CACHE STRING "The build type for flang")
set(OPTSCHEDPRJ_FLANG_LLVM_BUILD_TYPE ${OPTSCHEDPRJ_FLANG_BUILD_TYPE} CACHE STRING "The build type for flang-llvm")
set(OPTSCHEDPRJ_FLANG_DRIVER_BUILD_TYPE ${OPTSCHEDPRJ_FLANG_BUILD_TYPE} CACHE STRING "The build type for flang-driver")
set(OPTSCHEDPRJ_FLANG_OPENMP_BUILD_TYPE ${OPTSCHEDPRJ_FLANG_BUILD_TYPE} CACHE STRING "The build type for openmp")
set(OPTSCHEDPRJ_FLANG_LIBPGMATH_BUILD_TYPE ${OPTSCHEDPRJ_FLANG_BUILD_TYPE} CACHE STRING "The build type for libpgmath")
set(OPTSCHEDPRJ_FLANG_COMPILER_BUILD_TYPE ${OPTSCHEDPRJ_FLANG_BUILD_TYPE} CACHE STRING "The build type for the flang compiler")

set(OPTSCHEDPRJ_FLANG_CMAKE_GENERATOR ${CMAKE_GENERATOR} CACHE STRING "The generator to use to build flang")
set(OPTSCHEDPRJ_FLANG_LLVM_CMAKE_GENERATOR ${OPTSCHEDPRJ_FLANG_CMAKE_GENERATOR} CACHE STRING "The generator to use to build flang-llvm")
set(OPTSCHEDPRJ_FLANG_DRIVER_CMAKE_GENERATOR ${OPTSCHEDPRJ_FLANG_CMAKE_GENERATOR} CACHE STRING "The generator to use to build flang-driver")
set(OPTSCHEDPRJ_FLANG_OPENMP_CMAKE_GENERATOR ${OPTSCHEDPRJ_FLANG_CMAKE_GENERATOR} CACHE STRING "The generator to use to build openmp")
set(OPTSCHEDPRJ_FLANG_LIBPGMATH_CMAKE_GENERATOR ${OPTSCHEDPRJ_FLANG_CMAKE_GENERATOR} CACHE STRING "The generator to use to build libpgmath")
set(OPTSCHEDPRJ_FLANG_COMPILER_CMAKE_GENERATOR ${OPTSCHEDPRJ_FLANG_CMAKE_GENERATOR} CACHE STRING "The generator to use to build the flang compiler")

include(CMakeParseArguments)
include(ExternalProject)

function(add_variable OUT_LIST NAME)
  set(var "${${OUT_LIST}}")
  
  if(DEFINED ${NAME})
    get_property(type CACHE ${NAME} PROPERTY TYPE)
    if(NOT type)
      set(type UNINITIALIZED)
    endif()
    list(APPEND var -D${NAME}:${type}=${${NAME}})
  endif()

  set(${OUT_LIST} "${var}" PARENT_SCOPE)
endfunction()

function(add_variables OUT_LIST)
  set(list)
  
  foreach(var IN LISTS ARGN)
    add_variable(list ${var})
  endforeach()

  set(${OUT_LIST} "${list}" PARENT_SCOPE)
endfunction()

set(cache_default_args)
add_variables(cache_default_args
  CMAKE_CXX_COMPILER_LAUNCHER
  CMAKE_C_COMPILER_LAUNCHER
)

ExternalProject_Add(llvm
  SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/llvm-project/llvm
  CMAKE_ARGS
    -DLLVM_PARALLEL_LINK_JOBS=${LLVM_PARALLEL_LINK_JOBS}
    -DLLVM_ENABLE_PROJECTS='clang'
    -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}
    -DLLVM_TARGETS_TO_BUILD=X86
    -DLLVM_BUILD_TOOLS=ON
    -DLLVM_INCLUDE_TESTS=ON
    -DLLVM_OPTIMIZED_TABLEGEN=ON
  CMAKE_CACHE_DEFAULT_ARGS
    ${cache_default_args}
  USES_TERMINAL_CONFIGURE 1
  USES_TERMINAL_BUILD 1
  USES_TERMINAL_INSTALL 1
  # Don't install anything:
  INSTALL_COMMAND ""
)

if(OPTSCHEDPRJ_FLANG)
  if(NOT EXISTS ${CMAKE_CURRENT_SOURCE_DIR}/flang/CMakeLists.txt
      OR NOT EXISTS ${CMAKE_CURRENT_SOURCE_DIR}/flang-driver/CMakeLists.txt
      OR NOT EXISTS ${CMAKE_CURRENT_SOURCE_DIR}/flang-llvm/CMakeLists.txt
      OR NOT EXISTS ${CMAKE_CURRENT_SOURCE_DIR}/openmp/CMakeLists.txt)
    if(NOT OPTSCHEDPRJ_IGNORE_FLANG)
      message(WARNING "Flang is improperly checked-out. Either finish checking out flang, flang-driver,"
        " and flang-llvm; set OPTSCHEDPRJ_FLANG=OFF to disable building flang; or set OPTSCHEDPRJ_IGNORE_FLANG=ON"
        " disable this warning while continuing to auto-detect flang."
      )
    endif()
  else()

    ExternalProject_Add(flang-llvm
      SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/flang-llvm
      INSTALL_DIR ${OPTSCHEDPRJ_FLANG_INSTALL_PREFIX}
      CMAKE_GENERATOR ${OPTSCHEDPRJ_FLANG_LLVM_CMAKE_GENERATOR}
      CMAKE_ARGS
        -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
        -DCMAKE_BUILD_TYPE=${OPTSCHEDPRJ_FLANG_LLVM_BUILD_TYPE}
        -DLLVM_TARGETS_TO_BUILD=X86
        -DLLVM_BUILD_TOOLS=ON
        -DLLVM_INCLUDE_TESTS=ON
        -DLLVM_OPTIMIZED_TABLEGEN=ON
      CMAKE_CACHE_DEFAULT_ARGS
        ${cache_default_args}
      USES_TERMINAL_CONFIGURE 1
      USES_TERMINAL_BUILD 1
      USES_TERMINAL_INSTALL 1
    )

    ExternalProject_Add(flang-driver
      DEPENDS flang-llvm
      SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/flang-driver
      INSTALL_DIR ${OPTSCHEDPRJ_FLANG_INSTALL_PREFIX}
      CMAKE_GENERATOR ${OPTSCHEDPRJ_FLANG_DRIVER_CMAKE_GENERATOR}
      CMAKE_ARGS
        -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
        -DCMAKE_BUILD_TYPE=${OPTSCHEDPRJ_FLANG_DRIVER_BUILD_TYPE}
        -DLLVM_CONFIG=<INSTALL_DIR>/bin/llvm-config
        -DCLANG_ENABLE_STATIC_ANALYZER=ON
      CMAKE_CACHE_DEFAULT_ARGS
        ${cache_default_args}
      USES_TERMINAL_CONFIGURE 1
      USES_TERMINAL_BUILD 1
      USES_TERMINAL_INSTALL 1
    )

    ExternalProject_Add(openmp
      DEPENDS flang-driver
      SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/openmp
      INSTALL_DIR ${OPTSCHEDPRJ_FLANG_INSTALL_PREFIX}
      CMAKE_GENERATOR ${OPTSCHEDPRJ_FLANG_OPENMP_CMAKE_GENERATOR}
      CMAKE_ARGS
        -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
        -DCMAKE_BUILD_TYPE=${OPTSCHEDPRJ_FLANG_OPENMP_BUILD_TYPE}
        -DCMAKE_CXX_COMPILER=<INSTALL_DIR>/bin/clang++
        -DCMAKE_C_COMPILER=<INSTALL_DIR>/bin/clang
      CMAKE_CACHE_DEFAULT_ARGS
        ${cache_default_args}
      USES_TERMINAL_CONFIGURE 1
      USES_TERMINAL_BUILD 1
      USES_TERMINAL_INSTALL 1
    )

    ExternalProject_Add(libpgmath
      DEPENDS openmp
      SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/flang/runtime/libpgmath
      INSTALL_DIR ${OPTSCHEDPRJ_FLANG_INSTALL_PREFIX}
      CMAKE_GENERATOR ${OPTSCHEDPRJ_FLANG_LIBPGMATH_CMAKE_GENERATOR}
      CMAKE_ARGS
        -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
        -DCMAKE_BUILD_TYPE=${OPTSCHEDPRJ_FLANG_LIBPGMATH_BUILD_TYPE}
        -DCMAKE_CXX_COMPILER=<INSTALL_DIR>/bin/clang++
        -DCMAKE_C_COMPILER=<INSTALL_DIR>/bin/clang
        -DCMAKE_Fortran_COMPILER=<INSTALL_DIR>/bin/flang
      CMAKE_CACHE_DEFAULT_ARGS
        ${cache_default_args}
      USES_TERMINAL_CONFIGURE 1
      USES_TERMINAL_BUILD 1
      USES_TERMINAL_INSTALL 1
    )

    ExternalProject_Add(flang
      DEPENDS libpgmath
      SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/flang
      INSTALL_DIR ${OPTSCHEDPRJ_FLANG_INSTALL_PREFIX}
      CMAKE_GENERATOR ${OPTSCHEDPRJ_FLANG_COMPILER_CMAKE_GENERATOR}
      CMAKE_ARGS
        -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
        -DCMAKE_BUILD_TYPE=${OPTSCHEDPRJ_FLANG_COMPILER_BUILD_TYPE}
        -DCMAKE_CXX_COMPILER=<INSTALL_DIR>/bin/clang++
        -DCMAKE_C_COMPILER=<INSTALL_DIR>/bin/clang
        -DCMAKE_Fortran_COMPILER=<INSTALL_DIR>/bin/flang
        -DLLVM_CONFIG=<INSTALL_DIR>/bin/llvm-config
      CMAKE_CACHE_DEFAULT_ARGS
        ${cache_default_args}
      USES_TERMINAL_CONFIGURE 1
      USES_TERMINAL_BUILD 1
      USES_TERMINAL_INSTALL 1
    )
  endif()
endif()
